plugins {
    id "com.jfrog.bintray" version "1.6"
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'maven-publish'



group = 'org.batcha.aws.forks'
version = '1.0.1'

description = """Amazon SQS Java Messaging Library"""

sourceCompatibility = 1.7
targetCompatibility = 1.7



repositories {
        
     maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile group: 'com.amazonaws', name: 'aws-java-sdk-sqs', version:'1.9.6'
    compile group: 'org.apache.geronimo.specs', name: 'geronimo-jms_1.1_spec', version:'1.1.1'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'1.10.8'
}

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type:Jar) {
  classifier = 'javadoc'
  from javadoc.outputs.files  
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

javadoc {
  failOnError = false
}

test {
  ignoreFailures = true
}  

publishing {
	publications {
		main(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			
			pom.withXml {
				asNode().with {
					appendNode('description', project.description)
					appendNode('url', 'https://github.com/bat-cha/amazon-sqs-java-messaging-lib')
					appendNode('licenses').with {
						appendNode('license').with {
							appendNode('name', 'Apache-2.0 License')
							appendNode('url', 'https://raw.github.com/bat-cha/amazon-sqs-java-messaging-lib/master/LICENSE.txt')
						}
					}
					appendNode('developers').with {
						appendNode('developer').with {
							appendNode('id', 'bat-cha')
							appendNode('name', 'Baptiste Chatrain')
							appendNode('email', 'baptiste.chatrain@gmail.com')
						}
					}
					appendNode('scm').with {
						appendNode('connection', 'git://github.com/bat-cha/amazon-sqs-java-messaging-lib.git')
						appendNode('developerConnection', 'git@github.com:bat-cha/amazon-sqs-java-messaging-lib.git')
						appendNode('url', 'https://github.com/bat-cha/amazon-sqs-java-messaging-lib')
					}
				}
			}
		}
	}
	repositories {
		mavenLocal()
	}
}


bintray {

    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
 
	publications = ['main']
	pkg {
		repo = 'aws-forks'
   		name = 'org.batcha.aws.forks:amazon-sqs-java-messaging-lib'
   		desc = 'amazon-sqs-java-messaging-lib fork'
		licenses = ['Apache-2.0']
		labels = ['amazon-sqs-java-messaging-lib']
		vcsUrl = 'https://github.com/bat-cha/amazon-sqs-java-messaging-lib'
		version {
                name = '1.0.1'                
        }		
	}
}
